plugins {
    id "org.jetbrains.kotlin.jvm" version '1.8.0'
    id "org.jetbrains.kotlin.plugin.noarg" version '1.8.20'
    id "org.jetbrains.dokka" version '1.7.20'
    id "maven-publish"
}

group = 'xyz.malkki'
version = '0.2.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'

    implementation 'com.univocity:univocity-parsers:2.9.1'
}

noArg {
    annotation("xyz.malkki.gtfs.NoArgConstructor")
    invokeInitializers = true
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/mjaakko/gtfs-library")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

dokkaHtml {
    outputDirectory.set(file("${buildDir}/dokka"))

    dokkaSourceSets {
        configureEach {
            includes.from("docs/packages.md")

            jdkVersion.set(11)
        }
    }
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}

compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}
